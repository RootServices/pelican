plugins {
    id 'java'
    id 'maven-publish'
    id 'signing'
}

sourceCompatibility = 12
targetCompatibility = 12

version = '1.2.2-SNAPSHOT'
group = 'net.tokensmith'
description = 'framework to publish and consume messages'

ext{
    kafkaVersion = '2.5.0'
    jacksonVersion = '2.11.1'
    junitVersion = '5.0.0'
    log4jVersion = '2.13.3'
    slf4jVersion = '1.7.25'
}

repositories {
    maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
    maven { url "https://oss.sonatype.org/content/repositories/releases" }
    mavenCentral()
    mavenLocal()
}

configurations {
    deployerJars
}

dependencies {
    compile "org.apache.kafka:kafka-clients:${kafkaVersion}"

    compile "com.fasterxml.jackson.core:jackson-core:${jacksonVersion}"
    compile "com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}"
    compile "com.fasterxml.jackson.datatype:jackson-datatype-jdk8:${jacksonVersion}"
    compile "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:${jacksonVersion}"
    compile "com.fasterxml.jackson.module:jackson-module-afterburner:${jacksonVersion}"

    compile group: 'org.slf4j', name: 'slf4j-api', version: "${slf4jVersion}"

    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.apache.logging.log4j', name: 'log4j-slf4j-impl', version: "${log4jVersion}"
    testCompile group: 'org.apache.logging.log4j', name: 'log4j-api', version: "${log4jVersion}"
    testCompile group: 'org.apache.logging.log4j', name: 'log4j-core', version: "${log4jVersion}"

    deployerJars "org.apache.maven.wagon:wagon-http:2.2"
}


task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier = 'sources'
}

task javadocJar(type: Jar) {
    from javadoc
    archiveClassifier = 'javadoc'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'pelican'
            from components.java
            artifact sourcesJar
            artifact javadocJar

            pom {
                name = 'pelican'
                packaging = 'jar'
                description = 'framework to publish and consume messages'
                url = 'https://github.com/Tokensmith/pelican'

                scm {
                    connection = 'git@github.com:Tokensmith/pelican.git'
                    developerConnection = 'git@github.com:Tokensmith/pelican.git'
                    url = 'https://github.com/Tokensmith/otter'
                }

                licenses {
                    license {
                        name = 'The MIT License (MIT)'
                        url = 'https://github.com/Tokensmith/pelican/blob/development/LICENSE'
                    }
                }

                developers {
                    developer {
                        id = 'tmackenzie'
                        name = 'Tom MacKenzie'
                        email = 'tom@tokensmith.net'
                    }
                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl

            if(!project.ext.properties.containsKey("ossrhUser")) {
                project.ext.ossrhUser = 'wrongUser'
                project.ext.ossrhPassword = 'wrongPassword'
            }
            credentials {
                username = project.ext.ossrhUser
                password = project.ext.ossrhPassword
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}
