apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'signing'

sourceCompatibility = 12
targetCompatibility = 12

version = '1.1-SNAPSHOT'
project.group = 'org.rootservices'
archivesBaseName = "pelican"

ext{
    kafkaVersion = '1.0.0'
    jacksonVersion = '2.8.8'
    junitVersion = '5.0.0'
}

buildscript {
    repositories {
        mavenCentral()
    }
}

repositories {
    maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
    maven { url "https://oss.sonatype.org/content/repositories/releases" }
    mavenCentral()
    mavenLocal()
}

configurations {
    deployerJars
}

dependencies {
    compile "org.apache.kafka:kafka-clients:${kafkaVersion}"

    compile group: 'com.fasterxml.jackson.core', name: 'jackson-core',  version: '2.8.8'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind',  version: '2.8.8'
    compile group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jdk8',  version: '2.8.8'
    compile group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310',  version: '2.8.8'

    compile group: 'org.apache.logging.log4j', name: 'log4j-1.2-api', version: '2.7'
    compile group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.7'
    compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.7'
    compile group: 'org.apache.logging.log4j', name: 'log4j-slf4j-impl', version: '2.7'

    testCompile group: 'junit', name: 'junit', version: '4.12'
    deployerJars "org.apache.maven.wagon:wagon-http:2.2"
}


task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier = 'sources'
}

task javadocJar(type: Jar) {
    from javadoc
    archiveClassifier = 'javadoc'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'pelican'
            from components.java
            artifact sourcesJar
            artifact javadocJar

            pom {
                name = 'pelican'
                packaging = 'jar'
                description 'framework to publish and consume messages'
                url = 'https://github.com/RootServices/pelican'

                scm {
                    connection = 'git@github.com:RootServices/pelican.git'
                    developerConnection = 'git@github.com:RootServices/pelican.git'
                    url = 'https://github.com/RootServices/otter'
                }

                licenses {
                    license {
                        name = 'The MIT License (MIT)'
                        url = 'https://github.com/RootServices/pelican/blob/development/LICENSE'
                    }
                }

                developers {
                    developer {
                        id = 'tmackenzie'
                        name = 'Tom MacKenzie'
                        email = 'tom@rootservices.org'
                    }
                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl

            if(!project.ext.properties.containsKey("ossrhUser")) {
                project.ext.ossrhUser = 'wrongUser'
                project.ext.ossrhPassword = 'wrongPassword'
            }
            credentials {
                username = ossrhUser
                password =ossrhPassword
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}
